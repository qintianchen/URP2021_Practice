#pragma kernel CSMain

#include "AtmosphereScatteringUtils.hlsl"

RWTexture2D<float4> Result;

// 输出 TransmittanceLUT，横坐标对应 [PI, 0] 的天顶角，纵坐标对应 [0, AtmosphereHeight] 的海拔高度
[numthreads(8,8,1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
    AtmosphereParams params = _AtmosphereParamses[0];

    float w;
    float h;
    Result.GetDimensions(w, h);
    float2 uv = id.xy / float2(w, h);

    float r = params.planetRadius;
    
    float h1 = params.atmosphereHeight + r;
    float maxTangent2 = h1 * h1 - r * r; // 最大的切线长度为大气层顶部到星球表面的切线，用这个最大值来做归一化
    float height = sqrt(uv.y * uv.y * maxTangent2 + r * r) - r;

    float minCosTheta = - sqrt(maxTangent2) / (r + height);
    float cos_theta = uv.x * (1 - minCosTheta) + minCosTheta; // 对应 [minCosTheta, 1] 

    Result[id.xy] = float4(GetTransmittance(params, height, cos_theta), 1);
}
